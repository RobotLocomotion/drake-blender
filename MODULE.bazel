# SPDX-License-Identifier: BSD-2-Clause

module(name = "drake_blender")

bazel_dep(name = "platforms", version = "0.0.11")
bazel_dep(name = "rules_python", version = "1.4.0")

# For the most part, a given version of Blender only supports exactly one
# version of Python, so we'll pin ourselves to exactly that version.
PYTHON_VERSION = "3.10"

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(python_version = PYTHON_VERSION)
use_repo(python, "python_versions")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "requirements",
    python_version = PYTHON_VERSION,
    requirements_lock = "//:requirements.txt",
)
pip.parse(
    hub_name = "test_requirements",
    python_version = PYTHON_VERSION,
    requirements_lock = "//:test/requirements.txt",
)
pip.parse(
    hub_name = "examples_requirements",
    python_version = PYTHON_VERSION,
    requirements_lock = "//examples:requirements.txt",
)
use_repo(pip, "requirements")
use_repo(pip, "test_requirements")
use_repo(pip, "examples_requirements")

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

# This is a sample file from https://www.blender.org/download/demo-files/,
# licensed under CC0. Credit goes to Ramil Roosileht for creating it,
# https://twitter.com/limarest_art.
http_file(
    name = "color_attribute_painting",
    sha256 = "443b213229a4c863b2015beff623a700886c14928707a2fb24a6dd85fd80a207",
    urls = [
        base + "/demo/sculpt_mode/color_attribute_painting.blend"
        for base in [
            "https://mirrors.ocf.berkeley.edu/blender",
            "https://mirror.clarkson.edu/blender",
        ]
    ],
)

buildifier_repositories = use_extension("//tools:buildifier_repositories.bzl", "buildifier_repositories")
use_repo(buildifier_repositories, "buildifier-darwin-amd64")
use_repo(buildifier_repositories, "buildifier-darwin-arm64")
use_repo(buildifier_repositories, "buildifier-linux-amd64")
use_repo(buildifier_repositories, "buildifier-linux-arm64")
